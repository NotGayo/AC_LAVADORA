;====================================VARIABLES===========================================================
	
ESTADO EQU 0x20
EVENTO EQU 0x21
DISPLAY_1 EQU 0X25
DISPLAY_0 EQU 0x26

;------------------CUENTAS Y VALORES--------------

TIEMPO_RESTANTE_MINS EQU 0x22
DECENAS EQU 0x28
UNIDADES EQU 0x29
TEMP_SEL EQU 0x2A

PRIMERA_VUELTA EQU 0x27.5
CONT_INTERRUPCIONES_T0 EQU 0x2B
CONT_CUANTOS_500MS EQU 0x2C

;------------------TICKS--------------

TICK_INT_0 EQU 0x23.0
TICK_ADC_0 EQU 0x23.1
TICK_ADC_1 EQU 0x23.2
TICK_TIMER_0 EQU 0x23.3
TICK_1S EQU 0X27.0
TICK_15S EQU 0x27.1
TICK_1MIN EQU 0x27.2
TICK_50MIN EQU 0X27.3
TICK_10MIN EQU 0X27.4 

;-----------SENSORES Y ACTIVACIONES-----------------

BLOQUEO_PUERTA EQU P0.7
MOTOR_DESAGUE EQU P1.0
PUERTA_ABIERTA EQU P1.1
SENSOR_LLENADO EQU P1.2
SENSOR_VACIADO EQU P1.3
BOTON_INICIO EQU P1.4
BIT1_TEMP EQU P3.1
BIT0_TEMP EQU P3.0
VALV_ENTRADA_AGUA EQU P3.3
CALENTADOR_AGUA EQU P3.6
SENTIDO_GIRO_MOTOR EQU P2.7

;-------------REGISTROS IMPORTANTES-------------------------------

ADCON EQU 0xC5
ADCH EQU 0xC6
PWMP EQU 0xFE
PWM0 EQU 0xFC

;======================================INCIO================================================

ORG 0x0000
	AJMP INICIO
ORG 0X0003
	AJMP INT0_ISR
ORG 0X0053
	AJMP ADC_ISR
ORG 0X007b

INICIO:
	ACALL INICIALIZACIONES

MAIN:					;LAZO PRINCIPAL
	ACALL MAQ_ESTADOS
	AJMP MAIN

MAQ_ESTADOS:
	MOV A, ESTADO
	RL A
	MOV DPTR, #LISTA_EST
	JMP @A+DPTR
LISTA_EST:
	AJMP ESTADO_0 ; REPOSO
	AJMP ESTADO_1 ; COMPROBACION SOBREPESO
	AJMP ESTADO_2 ; PUERTA ABIERTA
	AJMP ESTADO_3 ; LLENANDO
	AJMP ESTADO_4 ; CALENTAR CUBA
	AJMP ESTADO_5 ; LAVADO
	AJMP ESTADO_6 ; VACIAR AGUA
	AJMP ESTADO_7 ; CENTRIFUGAR
	AJMP ESTADO_8 ; FIN

;================================INICIALIZACIONES====================================================================

INICIALIZACIONES:

	MOV ESTADO, #0
	MOV EVENTO, #0
	MOV DISPLAY_1, #0x05
	MOV DISPLAY_0, #0x00
	MOV TIEMPO_RESTANTE_MINS, #0x60	;INICIAMOS LOS MINUTOS RESTANTES DE LAVADO A 60 IREMOS RESTANDO DE UNO EN UNO
	CLR IE.7
	SETB P3.2	;DESACTIVAMOS A NIVEL BAJO LA INT0
	SETB IE.7	;ACTIVAMOS EA* TODAS LAS INTERRUPCIONES
	SETB IE.0	;ACTIVAMOS LA INT0	
	CLR TICK_INT_0 ;TICK A NIVEL ALTO POR ELLO SE INICIA A 0
	CLR TICK_ADC_0 ;TICK A NIVEL ALTO INICIAMOS A 0
	CLR TICK_1S
	CLR TICK_15S
	CLR TICK_1MIN
	CLR TICK_10MIN
      	CLR TICK_50MIN
	CLR PUERTA_ABIERTA	;EMPEZAMOS CON LA PUERTA CERRADA
	CLR PRIMERA_VUELTA	;
	CLR P0.0
	CLR P0.1
	CLR P0.2
	CLR P0.3
	CLR P0.4
	CLR P0.5
	CLR P0.6
	CLR P0.7

	CLR P1.0
	SETB P1.1
	CLR P1.2
	SETB P1.3
	CLR P1.4
	CLR P1.5
	CLR P1.6
	CLR P1.7

	CLR P2.0
	CLR P2.1
	CLR P2.2
	CLR P2.3
	CLR P2.4
	CLR P2.5
	CLR P2.6
	CLR P2.7
	
	CLR P3.0
	CLR P3.1

	CLR P3.3
	CLR P3.6
	
	MOV PWMP, 0x80


	RET	
;=====================================================================================================

;LLAMAMOS AL GENERADOR DE EVENTOS PARA QUE ASI NOS DIGA A CUAL DEBEMOS IR EN EL ESTADO DEL SISTEMA QUE NOS ENCONTRAMOS

ESTADO_0:
	ACALL GEN_EV_0
	ACALL MQ_EVEN_0 	;REPOSO
	RET

ESTADO_1:
	ACALL GEN_EV_1
	ACALL MQ_EVEN_1		;COMPROBANDO SP
	RET

ESTADO_2:
	ACALL GEN_EV_2
	ACALL MQ_EVEN_2
	RET

ESTADO_3:
	ACALL GEN_EV_3
	ACALL MQ_EVEN_3
	RET
ESTADO_4:
	ACALL GEN_EV_4
	ACALL MQ_EVEN_4
	RET

ESTADO_5:
	ACALL GEN_EV_5
	ACALL MQ_EVEN_5
	RET

ESTADO_6:
	ACALL GEN_EV_6
	ACALL MQ_EVEN_6
	RET

ESTADO_7:
	ACALL GEN_EV_7
	ACALL MQ_EVEN_7
	RET

ESTADO_8:
	ACALL GEN_EV_8
	ACALL MQ_EVEN_8
	RET


;===============================================ESTADO 0 - REPOSO======================================================
MQ_EVEN_0:

	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_0
	JMP @A+DPTR
LIST_EVEN_MQEV_0:
	AJMP ME0_EV_0 ; Evento 0
	AJMP ME0_EV_1 ; Evento 1
	AJMP ME0_EV_2 ; Evento 2
ME0_EV_0:
	ORL PCON, #01H 		;PONEMOS EL IDLE BIT 0 DE PCON NO DEBEMOS ACCEDER EN FORMA DE BIT HACEMOS UN ORL
	RET
ME0_EV_1:
	;---	;ENCENDIO Y PUERTA ABIERTA
	CLR IE.0
	MOV DISPLAY_0, #77H	;A
	MOV DISPLAY_1, #73H	;P
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1	
	MOV ESTADO, #02H
	RET
ME0_EV_2:
	;---	;ENCENDIO Y PUERTA CERRADA
	CLR IE.0
	SETB IE.7
	SETB IE.6
	ACALL ACTIVAR_ADC_PESO
	CLR P3.2
	MOV ESTADO, #01H
	RET

;===========================================ESTADO 1 - COMPROBANDO SP==========================================================
MQ_EVEN_1:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_1
	JMP @A+DPTR
LIST_EVEN_MQEV_1:
	AJMP ME1_EV_0 ; Evento 0
	AJMP ME1_EV_1 ; Se pulsa p3.2 apagar ir a estado 0
	AJMP ME1_EV_2 ; Se abre la puerta
	AJMP ME1_EV_3 ; salta adc y sobre peso
	AJMP ME1_EV_4 ; salta adc, peso ok y p1.4
ME1_EV_0:
	RET
ME1_EV_1:	;PULSA ENCENDIDO DE NUEVO
	MOV ESTADO, #0x00
	SETB IE.0
	CLR IE.6
	RET
ME1_EV_2:	;PUERTA ABIERTA
	MOV DISPLAY_0, #77H	;A
	MOV DISPLAY_1, #73H	;P
	MOV ESTADO, #02H
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1	
	RET
ME1_EV_3:	;HAY SOBREPESO
	MOV DISPLAY_0, #73H	;P
	MOV DISPLAY_1, #6DH	;S
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	RET
ME1_EV_4:	;NO SOBREPESO Y ENCENDIDO
	MOV DISPLAY_1, #3FH	;t
	SETB BLOQUEO_PUERTA
	ACALL CARGAR_TEMP_DSP_0 ;Esta subrutina carga en DISPLAY_1 el valor de la temperatura actual
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	SETB VALV_ENTRADA_AGUA
	MOV ESTADO, #03H
	RET
;==========================================ESTADO 2 - PUERTA ABIERTA===========================================================

MQ_EVEN_2:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_2
	JMP @A+DPTR
LIST_EVEN_MQEV_2:
	AJMP ME2_EV_0 ; Evento 0
	AJMP ME2_EV_1 ; Descripci?n Maq. Event. 0. Evento 1
ME2_EV_0:
	RET
ME2_EV_1:	;PUERTA CERRADA
	MOV ESTADO, #01H
	ACALL CARGAR_TEMP_DSP_0 ;Esta subrutina carga en DISPLAY_1 el valor de la temperatura actual
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	RET

;==========================================ESTADO 3 - LLENADO===========================================================

MQ_EVEN_3:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_3
	JMP @A+DPTR
LIST_EVEN_MQEV_3:
	AJMP ME3_EV_0 		; Evento 0
	AJMP ME3_EV_1 
ME3_EV_0:
	RET
ME3_EV_1:			;CUBA LLENA
	MOV ESTADO, #04H
	SETB CALENTADOR_AGUA
	ACALL ACTIVAR_ADC_TEMP
	ACALL ACTIVAR_TIMER_0
	CLR TICK_1S
	CLR VALV_ENTRADA_AGUA
	ACALL SET_TEMP_SELECT
	RET

;==========================================ESTADO 4 - CALENTAMIENTO CUBA===========================================================

MQ_EVEN_4:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_4
	JMP @A+DPTR
LIST_EVEN_MQEV_4:
	AJMP ME4_EV_0 ; Evento 0
	AJMP ME4_EV_1
	AJMP ME4_EV_2 
ME4_EV_0:
	RET
ME4_EV_1:	; NO CALIENTE
	ACALL ACTIVAR_TIMER_0
	RET
ME4_EV_2:	; AGUA CALIENTE
	MOV ESTADO, #05H
	CLR CALENTADOR_AGUA
	ACALL ACTIVAR_TIMER_0
	ACALL ACTIVAR_PWM0_60RPM
	RET

;==========================================ESTADO 5 - LAVADO===========================================================

MQ_EVEN_5:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_5
	JMP @A+DPTR
LIST_EVEN_MQEV_5:
	AJMP ME5_EV_0 ; Evento 0
	AJMP ME5_EV_1
	AJMP ME5_EV_2
	AJMP ME5_EV_3 
ME5_EV_0:
	RET
ME5_EV_1:	; PASA 15S
	ACALL CAMBIAR_SENTIDO_GIRO
	ACALL ACTIVAR_TIMER_0
	ACALL MAPEAR_TIEMPO_D1_D0		;ACTUALIZAMOS PARA PODER MODIFICAR EL PUERTO 2.7
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	RET
ME5_EV_2:	; PASAN 1 MIN
	DEC TIEMPO_RESTANTE_MINS
	ACALL MAPEAR_TIEMPO_D1_D0		;GUARDA EN DSP1 Y DSP0 LOS VALORES DEL TIEMPO
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	ACALL ACTIVAR_TIMER_0		;VOLVEMOS A ACTIVAR LA CUENTA DE 15S
	RET
ME5_EV_3:	; PASAN 50 MIN
	ACALL MAPEAR_TIEMPO_D1_D0		;GUARDA EN DSP1 Y DSP0 LOS VALORES DEL TIEMPO
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	ACALL APAGAR_TIMER
	MOV ESTADO, #06H
	SETB MOTOR_DESAGUE
	RET

;==========================================ESTADO 6 - VACIAR AGUA===========================================================

MQ_EVEN_6:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_6
	JMP @A+DPTR
LIST_EVEN_MQEV_6:
	AJMP ME6_EV_0 ; Evento 0
	AJMP ME6_EV_1
ME6_EV_0:
	RET
ME6_EV_1:	; CUBA VACIA
	ACALL ACTIVAR_PWM0_600RPM
	ACALL ACTIVAR_TIMER_0
	SETB SENTIDO_GIRO_MOTOR
	ACALL MAPEAR_TIEMPO_D1_D0
	ACALL CARGAR_DISPLAY_1
	ACALL CARGAR_DISPLAY_0
	RET

;==========================================ESTADO 7 - CENTRIFUGAR===========================================================

MQ_EVEN_7:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_7
	JMP @A+DPTR
LIST_EVEN_MQEV_7:
	AJMP ME7_EV_0 	; Evento 0
	AJMP ME7_EV_1	; PASAN LOS 10MIN ACABAMOS EL CENTRIF.
	AJMP ME7_EV_2	; ACTUALIZAR TIEMPO Y NO 10 MINS DE CENTRIF.
ME7_EV_0:
	RET
ME7_EV_1:	; PASAN 10 MIN
	MOV ESTADO, #08H
	ACALL APAGAR_PWM0
	CLR BLOQUEO_PUERTA
	MOV DISPLAY_1, 0x38	;L
	MOV DISPLAY_0, 0x71	;F
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1 
	RET

ME7_EV_2:	; PASA 1 MIN
	ACALL MAPEAR_TIEMPO_D1_D0		;GUARDA EN DSP1 Y DSP0 LOS VALORES DEL TIEMPO
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1
	RET

;==========================================ESTADO 8 - FIN===========================================================

MQ_EVEN_8:
	MOV A, EVENTO
	RL A
	MOV DPTR, #LIST_EVEN_MQEV_8
	JMP @A+DPTR
LIST_EVEN_MQEV_8:
	AJMP ME8_EV_0 ; Evento 0
	AJMP ME8_EV_1
ME8_EV_0:
	RET
ME8_EV_1:			; PUERTA ABIERTA
	MOV DISPLAY_0, #77H	; A
	MOV DISPLAY_1, #73H	; P
	MOV ESTADO, #02H
	ACALL CARGAR_DISPLAY_0
	ACALL CARGAR_DISPLAY_1	
	RET

;==============================================GENERADORES DE EVENTOS=======================================================

;-------------------------------------------------------0-----------------------------------------------------------

GEN_EV_0:
	;CONDICIONES ESTADO 0
	JNB P3.2, COMPROBAR_PUERTA	;SI NO ESTA EL BOTON NI NOS MOLESTAMOS EN VER EL RESTO DE CONDICIONES 
	MOV EVENTO, #00H		;ESTAMOS SEGURO EN ESTADO 0 SI NO ESTA P3.2
	RET

COMPROBAR_PUERTA:
	JB PUERTA_ABIERTA, GEN_EV_1_ES_0
	AJMP GEN_EV_2_ES_0

GEN_EV_1_ES_0:
	ANL PCON, #0xFE 		;QUITAMOS EL IDLE MODIFICANDO EL BIT 0 DEL PCON
	MOV EVENTO, #01H		;EN EL CASO DE QUE SI ESTE LA PUERTA ABIERTA EVENTO 1 ESTADO 0
	RET

GEN_EV_2_ES_0:
	ANL PCON, #0xFE			;QUITAMOS EL IDLE MODIFICANDO EL BIT 0 DEL PCON
	MOV EVENTO, #02H		;EN EL CASO DE QUE SI ESTE LA PUERTA ABIERTA EVENTO 2 ESTADO 0
	RET



;-------------------------------------------------------1------------------------------------------------------------

GEN_EV_1:
	;CONDICIONES ESTADO 1
	JB P3.2, GEN_EV_1_ES_1
	JB PUERTA_ABIERTA, GEN_EV_2_ES_1
	;ADC
	JB TICK_ADC_0, HAY_SP
	MOV EVENTO,#00H
	RET

GEN_EV_1_ES_1:
	MOV EVENTO, #01H
	RET
GEN_EV_2_ES_1:
	MOV EVENTO, #02H
	RET
GEN_EV_3_ES_1:
	CLR TICK_ADC_0
	MOV EVENTO, #03H
	RET
GEN_EV_4_ES_1:
	CLR TICK_ADC_0
	MOV EVENTO, #04H
	RET

HAY_SP:
	MOV A, ADCH
	SUBB A, #00H; <- 00h sust. VALOR SP
	JC GEN_EV_3_ES_1 	;SI EL RESULTADO ES NEGATIVO (HAY CARRY) HAY SOBREPESO
	JB BOTON_INICIO, GEN_EV_4_ES_1	;SI NO SI ESTA INICIO PULSADO PASAMOS AL EVENTO 4
	RET
	
;------------------------------------------------------2------------------------------------------------------------

GEN_EV_2:
	;CONDICIONES ESTADO 0
	JNB PUERTA_ABIERTA,  GEN_EV_1_ES_2
	MOV EVENTO, #00H
	RET

GEN_EV_1_ES_2:
	MOV EVENTO, #01H
	RET


;--------------------------------------------------------3----------------------------------------------------------

GEN_EV_3:
	;CONDICIONES ESTADO 3
	JB SENSOR_LLENADO, GEN_EV_1_ES_3
	MOV EVENTO, #00H
	RET

GEN_EV_1_ES_3:
	MOV EVENTO, #01H
	RET

;----------------------------------------------------------4--------------------------------------------------------

GEN_EV_4:
	;CONDICIONES ESTADO 44
	JB TICK_1S, COMPROBAR_TEMP
	MOV EVENTO, #00H
	RET

COMPROBAR_TEMP:
	JB P3.1, COMP_T3_T2_T1
	JB P3.0, COMP_T3_T2_T1
	AJMP GEN_EV_2_ES_4			;SI TEMP1 - 0 Y TEMP0 - 0 ENTONCES T0 Y COMO ESTE ESTA OK
	RET

	COMP_T3_T2_T1:				;SOLO SET TEMP_SEL SI T1,T2,T3 YA QUE T0 USA AGUA TAL CUAL
		MOV A, ADCH
		SUBB A, TEMP_SEL 		; SUSTITUIR EL VALOR DE 00H POR TEMP MINA ALCANZAR
		JC GEN_EV_1_ES_4
		AJMP GEN_EV_2_ES_4
		RET

GEN_EV_1_ES_4:				; NO CALIENTE
	CLR TICK_1S
	MOV EVENTO, #01H
	RET

GEN_EV_2_ES_4:				; SI CALIENTE
	CLR TICK_1S
	MOV EVENTO, #02H
	RET
;------------------------------------------------------------------------------------------------------------------

GEN_EV_5:
	;CONDICIONES ESTADO 0
	JB TICK_50MIN, GEN_EV_3_ES_5
	JB TICK_1MIN, GEN_EV_2_ES_5
	JB TICK_15S, GEN_EV_1_ES_5
	MOV EVENTO, #00H
	RET

GEN_EV_1_ES_5:	;15 SEG
	CLR TICK_15S
	MOV EVENTO,#01H
	RET

GEN_EV_2_ES_5:	;1MIN
	CLR TICK_1MIN
	MOV EVENTO,#02H
	RET

GEN_EV_3_ES_5:	;50MIN
	CLR TICK_50MIN
	MOV EVENTO,#03H
	RET

;------------------------------------------------------------------------------------------------------------------

GEN_EV_6:
	;CONDICIONES ESTADO 0
	JB SENSOR_VACIADO, GEN_EV_1_ES_6
	MOV EVENTO, #00H
	RET

GEN_EV_1_ES_6:
	MOV EVENTO, #01H
	RET
;------------------------------------------------------------------------------------------------------------------

GEN_EV_7:
	;CONDICIONES ESTADO 7
	JB TICK_10MIN, GEN_EV_1_ES_7
	JB TICK_1MIN, GEN_EV_2_ES_7
	MOV EVENTO, #00H
	RET

GEN_EV_1_ES_7:
	CLR TICK_10MIN
	MOV EVENTO, #01H
	RET

GEN_EV_2_ES_7:
	CLR TICK_1MIN
	MOV EVENTO, #02H
	RET
;------------------------------------------------------------------------------------------------------------------

GEN_EV_8:
	;CONDICIONES ESTADO 0
	JB PUERTA_ABIERTA, GEN_EV_1_ES_8
	MOV EVENTO, #00H
	RET

GEN_EV_1_ES_8:
	MOV EVENTO, #01H
	RET

;------------------------------------------------------------------------------------------------------------------
;================================================ACCIONES=======================================================

ACTIVAR_ADC_PESO:			;SELECCIONAMOS EL CANAL 0 PARA PESO
	ANL ADCON,#0xE8
	ORL ADCON,#0x08
	RET

ACTIVAR_ADC_TEMP:			;SELECCIONAMOS EL CANAL 1 PARA TEMP
	ANL ADCON, #0xE6
	ORL ADCON, #0x09
	RET
APAGAR_ADC:
	ANL IE, #0xBF
	RET


ACTIVAR_TIMER_0:
	JNB PRIMERA_VUELTA, ACTIVAR_TIMER_0_PRIMERA_VUELTA
	AJMP ACTIVAR_TIMER_0_FULL
	RET

ACTIVAR_TIMER_0_PRIMERA_VUELTA:
	MOV TMOD, #0x01
	MOV TH0, #0x7B
	MOV TL0, #0x80
	SETB IE.7
	SETB IE.1
	SETB PRIMERA_VUELTA
	SETB TR0
	RET

ACTIVAR_TIMER_0_FULL:
	MOV TMOD, #0x01
	MOV TH0, #0x00
	MOV TL0, #0x00
	SETB IE.7
	SETB IE.1
	SETB TR0
	RET

ACTUALIZAR_CONT_TIMER_0:
	INC CONT_INTERRUPCIONES_T0
	
	;SI CONT_INT_TO = 31 ENTONCES tick_1s
	;si tick_1s inc cont 1s
	;si cont1_1s = 15 ENTONCES tick_15s
	;si tick 15s inc cont_15s
	;si cont_15s = 4 entonces tick_1min
	;si tick_1min inc cont_1min
	;si cont_1min = 10 ENTONCES tick_10min
	;si tick_10min inc cont_10min
	;si cont_10min = 5 ENTONCES tick_50min


	RET
APAGAR_TIMER:
	RET

ACTIVAR_PWM0_60RPM:
	MOV PWM0, 0x1A
	RET

ACTIVAR_PWM0_600RPM:
	MOV PWM0, 0xE6
	RET

APAGAR_PWM0:
	MOV PWM0, 0x00
	RET

CARGAR_DISPLAY_1:
	MOV A, #0x80
	ANL P0, A		; 1000 0000 -> nos settea todo a 0 menos el .7
	MOV A, DISPLAY_1
	ORL P0, A		; dsp1 siempre tiene un 0 en .7 por ello no modifica p0.7
	RET

CARGAR_DISPLAY_0:
	MOV A, #0x80
	ANL P2, A		; 1000 0000 -> nos settea todo a 0 menos el .7
	MOV A, DISPLAY_0
	ORL P2, A		; dsp1 siempre tiene un 0 en .7 por ello no modifica p0.7
	RET


SET_TEMP_SELECT:
	JB P3.1, SET_T_ALTAS
	AJMP SET_T_BAJAS
	RET
	
	SET_T_ALTAS:
		JB P3.0, SET_T_T3
		AJMP SET_T_T2
		RET
	SET_T_BAJAS:
		JB P3.0, SET_T_T1
		RET

		SET_T_T3:
			MOV TEMP_SEL, #0xCD
			RET
		SET_T_T2:
			MOV TEMP_SEL, #0x9A
			RET
		SET_T_T1:
			MOV TEMP_SEL, #0x66
			RET
	
CARGAR_TEMP_DSP_0:
	JNB BIT1_TEMP, COMPROBAR_B0T_B1_0
	AJMP COMPROBAR_B0T_B1_1
	RET

	COMPROBAR_B0T_B1_0:
		JB BIT0_TEMP, PINTAR_D0_T1
		AJMP PINTAR_D0_T0
		RET
	COMPROBAR_B0T_B1_1:
		JB BIT0_TEMP, PINTAR_D0_T3
		AJMP PINTAR_D0_T2
		RET
	
	PINTAR_D0_T0:
		MOV DISPLAY_0, #0x3F	;0
		RET
	PINTAR_D0_T1:
		MOV DISPLAY_0, #0x06	;1
		RET
	PINTAR_D0_T2:
		MOV DISPLAY_0, #0x5B	;2
		RET
	PINTAR_D0_T3:
		MOV DISPLAY_0, #0x4F	;3
		RET

MAPEAR_TIEMPO_D1_D0:			;guardamos los valores de t restante por decenas y unidades en dos dir.
	MOV A,TIEMPO_RESTANTE_MINS
	MOV B, #0AH
	DIV AB				; APROVECHAMOS QUE ADCH/10 COCIENTE -> A (decenas) Y RESTO -> B (unidades)
	MOV DECENAS, A
	MOV UNIDADES, B
	
	ACALL MAP_DEC_D1
	ACALL MAP_UNI_D0

	RET

	MAP_DEC_D1:
		MOV A, DECENAS
		RL A
		MOV DPTR, #LISTA_DECENAS
		JMP @A+DPTR
	LISTA_DECENAS:
		AJMP CD0
		AJMP CD1
		AJMP CD2
		AJMP CD3
		AJMP CD4
		AJMP CD5
		AJMP CD6
		AJMP CD7
		AJMP CD8
		AJMP CD9
	
	CD0:
		MOV DISPLAY_1, #0x3F
		RET
	CD1:
		MOV DISPLAY_1, #0x06
		RET
	CD2:
		MOV DISPLAY_1, #0x5B
		RET
	CD3:
		MOV DISPLAY_1, #0x4F
		RET
	CD4:
		MOV DISPLAY_1, #0x66
		RET
	CD5:
		MOV DISPLAY_1, #0x6D
		RET
	CD6:
		MOV DISPLAY_1, #0x7D
		RET
	CD7:
		MOV DISPLAY_1, #0x07
		RET
	CD8:
		MOV DISPLAY_1, #0x7F
		RET
	CD9:
		MOV DISPLAY_1, #0x6F
		RET

	MAP_UNI_D0:
		MOV A, UNIDADES
		RL A
		MOV DPTR, #LISTA_UNIDADES
		JMP @A+DPTR
	LISTA_UNIDADES:
		AJMP CU0
		AJMP CU1
		AJMP CU2
		AJMP CU3
		AJMP CU4
		AJMP CU5
		AJMP CU6
		AJMP CU7
		AJMP CU8
		AJMP CU9
	
	CU0:
		MOV DISPLAY_0, #0x3F
		RET
	CU1:
		MOV DISPLAY_0, #0x06
		RET
	CU2:
		MOV DISPLAY_0, #0x5B
		RET
	CU3:
		MOV DISPLAY_0, #0x4F
		RET
	CU4:
		MOV DISPLAY_0, #0x66
		RET
	CU5:
		MOV DISPLAY_0, #0x6D
		RET
	CU6:
		MOV DISPLAY_0, #0x7D
		RET
	CU7:
		MOV DISPLAY_0, #0x07
		RET
	CU8:
		MOV DISPLAY_0, #0x7F
		RET
	CU9:
		MOV DISPLAY_0, #0x6F
		RET

CAMBIAR_SENTIDO_GIRO:
	JB SENTIDO_GIRO_MOTOR, FALSE_SG			;SI SENT 1 -> VAMOS A SET0
	AJMP TRUE_SG					;SI SENT 0 -> SET1
	RET
	FALSE_SG:
		CLR SENTIDO_GIRO_MOTOR
		RET
	TRUE_SG:
		SETB SENTIDO_GIRO_MOTOR
		RET
;==============================================INTERRUPCIONES=======================================================

INT0_ISR:
	PUSH ACC
	PUSH PSW
	SETB TICK_INT_0
	CLR IE.0
	POP PSW
	POP ACC
	RETI

ADC_ISR:
	PUSH ACC
	PUSH PSW
	SETB TICK_ADC_0
	ANL ADCON, #0XF8
	POP PSW
	POP ACC
	RETI

TIMER_0_ISR:
	PUSH ACC
	PUSH PSW
	ACALL ACTUALIZAR_CONT_TIMER_0
	ACALL ACTIVAR_TIMER_0
	POP PSW
	POP ACC
	RETI

;==============================================FIN======================================================

END